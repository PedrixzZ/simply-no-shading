plugins {
	id 'com.matthewprenger.cursegradle' version '1.+'
	id 'com.modrinth.minotaur' version '2.+'
	id 'fabric-loom' version '1.+'
	id 'io.github.juuxel.loom-quiltflower' version 'latest.release'
}

apply from: 'mods.gradle'

javaVersion = 17 // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
loader = 'fabric'
gameVersion = '1.18.2'

group = 'com.github.startsmercury'
archivesBaseName = 'simply-no-shading'
version = "5.0.4+$gameVersion"

repositories {
	maven { url 'https://api.modrinth.com/maven'; content { includeGroup 'maven.modrinth' } }
	maven { url 'https://jitpack.io' }
	maven { url 'https://maven.gegy.dev' }
	maven { url 'https://maven.parchmentmc.org' }
	maven { url 'https://maven.quiltmc.org/repository/release' }
	maven { url 'https://maven.shedaniel.me' }
	maven { url 'https://maven.terraformersmc.com' }
	maven { url 'https://storage.googleapis.com/devan-maven' }
}

dependencies {
	// BedrockIfy
	modImplementation modrinth.latestDependency('bedrockify')

	// Enhanced Block Entities
	modImplementation modrinth.latestDependency('ebe')

	// Fabric Key Binding API (V1)
	modImplementation fabricApi.module('fabric-key-binding-api-v1')

	// Fabric Lifecycle Events (V1)
	modImplementation fabricApi.module('fabric-lifecycle-events-v1')

	// Fabric Renderer API (V1)
	modImplementation fabricApi.module('fabric-renderer-api-v1')

	// Fabric Loader
	modImplementation 'net.fabricmc:fabric-loader:latest.release'

	// Iris
	modImplementation modrinth.latestDependency('iris')

	// Layered Mappings
	mappings loom.layered {
		officialMojangMappings()
		parchment parchment.latestDependency()
	}

	// Minecraft
	minecraft "com.mojang:minecraft:$gameVersion"

	// ModMenu
	modImplementation modrinth.latestDependency('modmenu')

	// Sodium
	modImplementation modrinth.latestDependency('sodium')

	// SpruceUI
	modImplementation include('dev.lambdaurora:spruceui:[3.3.3,4.0.0)')
}

processResources {
	inputs.property 'version', version

	filesMatching 'fabric.mod.json', {
		expand version: version
	}
}

java {
	withJavadocJar()
	withSourcesJar()
}

jar {
	from 'LICENSE', {
		rename { "${it}_$project.archivesBaseName" }
	}
}

/************************/
/** Java Compatibility **/
/************************/

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

tasks.withType JavaCompile configureEach {
	options.release = project.ext.javaVersion
}

/****************/
/** Publishing **/
/****************/
import com.modrinth.minotaur.dependencies.ModDependency

final def versionPart = version.split(/\+/)[0]
final def versionNameParts = versionPart.split('-').with { it.size() == 2 ? [ it[0], *it[1].split(/\./) ] : it }.tap {
	if (it.size() == 1) return

	switch (it[1]) {
	case 'rc':
		it[1] = 'Release Candidate'
		break
	case 'pre':
		it[1] = 'Pre-Release'
		break
	case 'snapshot':
		it[1] = 'Snapshot'
		break
	}
}
final def versionNamePart = versionNameParts.join ' '
final def versionNeme = "$versionNamePart for Minecraft $gameVersion"
final def changelag = "**View the changelog at**: https://github.com/${System.getenv 'GITHUB_REPOSITORY'}/wiki/Changelog#${versionPart.replace '.', ''}"
final def varsionType = versionNameParts.with {
	switch (it.size() == 1 ? 'Release' : it[1]) {
	case 'Release':
		return 'release'
	case 'Release Candidate':
	case 'Pre-Release':
		return 'beta'
	default:
		return 'alpha'
	}
}

curseforge {
	apiKey = System.getenv('CURSEFORGE_API_KEY') ?: ''

	project {
		id = '550997'
		changelogType = 'markdown'
		changelog = changelag
		relations {
			optionalDependency 'bedrockify'
			optionalDependency 'enhanced-block-entities'
			optionalDependency 'fabric-api'
			optionalDependency 'irisshaders'
			optionalDependency 'modmenu'
			optionalDependency 'sodium'
		}
		addGameVersion gameVersion
		addGameVersion "Java $javaVersion"
		addGameVersion loader.with {
			switch (it) {
				case 'fabric':
					return 'Fabric'
				case 'forge':
					return 'Forge'
				case 'quilt':
					return 'Quilt'
				case 'rift':
					return 'Rift'
			}
		}
		releaseType = varsionType

		mainArtifact remapJar, {
			displayName = versionNeme
		}
		addArtifact javadocJar
		addArtifact remapSourcesJar

		afterEvaluate {
			uploadTask.dependsOn 'javadocJar', 'remapJar', 'remapSourcesJar'
		}
	}

	options {
		forgeGradleIntegration = false
	}
}

modrinth {
	token = System.getenv 'MODRINTH_TOKEN'
	projectId = '9gx5Xvc5'
	versionName = versionNeme
	versionNumber = version
	changelog = changelag
	dependencies = [
		new ModDependency('ox3rDp1B', 'optional'),
		new ModDependency('OVuFYfre', 'optional'),
		new ModDependency('P7dR8mSH', 'optional'),
		new ModDependency('YL57xq9U', 'optional'),
		new ModDependency('mOgUt4GM', 'optional'),
		new ModDependency('AANobbMI', 'optional')
	]
	gameVersions = [ gameVersion ]
	versionType = varsionType
	loaders = [ loader ]
	uploadFile = remapJar
	additionalFiles = [ remapSourcesJar ]
}
